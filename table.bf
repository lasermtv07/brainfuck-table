~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
brainfuck HTML table generator
(c) lasermtv07 2024
I hereby unleash the software to the world under
the terms of WTFPL by Sam Hocevar See their website
wtfpl dot net
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~{print &lt table &gt}~~~~~~~~~~~~~~~~~~~
+++++ +++++[>+++++ +<-]>.
<+++++ +++++[>+++++ +<-]>----.
----- ----- ----- ----.+.
+++++ +++++.----- --.
<+++++ +++++[>----<-]>+.
[-]+++++ +++++.[-]<
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
,>,>,>,[-],>,>, ~load the input
>>++++++[<+++++ +++>-]< ~save 0x30 for converting ascii to int
[<-<-<-<-<-<->>>>>>-] ~remove 0x30 from all cells
<<<<<<
[ ~convert hundreds from the first number
	>>>>>>>
    >+++++ +++++[<+++++ +++++>-]
    <<<<<<<<-
]>
[ ~convert tens from the first number
	>>>>>>
    +++++ +++++
    <<<<<<-
]>
[ ~convert ones from the first number
	>>>>>
    + <<<<<-
]>
[ ~convert hundreds from the second number
	>>>>>
    >+++++ +++++[<+++++ +++++>-]
    <<<<<<-
]>
[ ~convert ones from the second number
	>>>>
    +++++ +++++
    <<<<-
]>
[ ~convert ones from the second number
	>>>
    + <<<-
]
>>>
[<<+>>-] ~i forgor so it complicated now
<
[

	>>
	~~~{print &lt tr &gt}~~~~~~~~~~~~~~~~~~~~~~
	+++++ +++++[>+++++ +<-]>.
	<+++++ +++[>+++++ ++<-]>.
	--.[-]<
	+++++ +++++[>+++++ +<-]>++.
	[-]
	+++++ +++++.
	[-]<
	~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	<
    <<[>>+<<-]>>
	[
    	>
        ~~~{prints &lt td &gt &lt /td &gt}~~~~~
		+++++ +++++[>+++++ +<-]>.
		<+++++ +++[>+++++ ++<-]>.
		----- ----- ----- -.
		[-]<+++++ +++++[>+++++ +<-]>++.
		[-]<+++++ +++[>++++<-]>.
		[-]<+++++ +++++[>+++++ +<-]>.
		----- ----- ---.
		+++++ +++++ +++<+++++ +++[>+++++ ++<-]>.
		----- ----- ----- -.
		[-]<+++++ +++++[>+++++ +<-]>++.
		[-]+++++ +++++.[-]<
		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        <-
        <<+>>
    ]
    >
		+++++ +++++[>+++++ +<-]>.
		----- ----- ---.<
		+++++ +++++ +[>+++++ +<-]>+++.
		--.[-]<
		+++++ +++++[>+++++ +<-]>++.
		[-]<
		+++++ +++++.
		[-]
	<<-
]

~~~{prints &lt table &gt}~~~~~~~~~~~~~~~~~~~~~~~~
+++++ +++++[>+++++ +<-]>.
----- ----- ---.
+++++ +++++ +++
<+++++ +++++[>+++++ +<-]>----.
----- ----- ----- ----.+.
+++++ +++++.----- --.
<+++++ +++++[>----<-]>+.
[-]<
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
